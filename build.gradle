buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
    }
}

plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.with {
    group = 'org.openended'
    version = "${System.env.TRAVIS_BUILD_NUMBER ?: 'DEV-SNAPSHOT'}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:1.5.13.RELEASE'
    }
}

dependencies {
    compile('org.apache.mahout:mahout-integration:0.13.0') {
        exclude group: 'org.apache.hbase'
        exclude group: 'org.apache.hadoop'
        exclude group: 'org.apache.lucene'
        exclude group: 'org.apache.solr'
        exclude group: 'com.thoughtworks.xstream'
        exclude group: 'commons-cli'
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compile('org.springframework:spring-jdbc')
    compile('org.springframework:spring-webmvc')
    compileOnly('org.springframework.boot:spring-boot-actuator')
    compileOnly('org.projectlombok:lombok')

    testCompile('org.mockito:mockito-core')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-starter-actuator')
    testCompile('org.lazyluke:log4jdbc-remix:0.2.7')
    testCompileOnly('org.projectlombok:lombok')
    testRuntime('org.springframework.boot:spring-boot-starter-web')
    testRuntime('com.h2database:h2')
}
